;====================================================================
; Main.asm file generated by New Project wizard
;
; Created:   Wed Sep 24 2025
; Processor: 8086
; Compiler:  MASM32
;
; Before starting simulation set Internal Memory Size 
; in the 8086 model properties to 0x10000
;====================================================================
; starts at 1 increments by 5 AFTER 5 minute mark
;====================================================================
DATA SEGMENT
    PORTA       EQU 0F0H   ; LCD data bus
    PORTB       EQU 0F2H   ; LCD control lines
    PORTC       EQU 0F4H   ; Keypad interface
    COM_REG     EQU 0F6H   ; 8255 command register

    PORTA_2       EQU 0C0H   ; LED FOR LOCK AND UNLOCK 1st bit IF LOCKED 2nd bit if UNLOCKED
    PORTB_2       EQU 0C2H   ; PB0 CONTROLS SERVO MOTOR
    PORTC_2       EQU 0C4H   ; 
    COM_REG_2     EQU 0C6H   ; 8255 command register

    PIC1        EQU 0E8H ; 8259 command port (A1 = 0)
    PIC2        EQU 0EAH ; 8259 data port (A1 = 1)
    ICW1        EQU 013H ; edge triggered, ICW4 required
    ICW2        EQU 080H ; vector base 80H
    ICW4        EQU 003H ; AEOI enabled, 8086 mode
    OCW1_MASK   EQU 11100000B ; enable IR0-IR4, mask others

    PIT_CH0  EQU 0F8H ; 8253 Channel 0
    PIT_CTRL EQU 0FEH ; 8253 Control Word Register

    KEY_ONE     EQU 00H
    KEY_TWO     EQU 01H
    KEY_THREE   EQU 02H
    KEY_FOUR    EQU 04H
    KEY_FIVE    EQU 05H
    KEY_SIX     EQU 06H
    KEY_SEVEN   EQU 08H
    KEY_EIGHT   EQU 09H
    KEY_NINE    EQU 0AH
    KEY_STAR    EQU 0CH
    KEY_ZERO    EQU 0DH
    KEY_HASH    EQU 0EH

    MIN_CODE_LEN    EQU 4
    MAX_CODE_LEN    EQU 8

    POWER_ON_HOLD_TICKS  EQU 30   ; adjust to match actual 3 s 
    POWER_OFF_HOLD_TICKS EQU 50   ; adjust to match actual 5 s 
    MESSAGE_PAUSE_MS     EQU 1500   ; generic pause after messages (approx ms)
    FLASH_HALF_MS        EQU 50    ; blink half-period (approx ms)
    DISARM_IDLE_TIMEOUT_MS  EQU 4000 ; inactivity limit (ms) for disarm prompt
    PAYMENT_IDLE_TIMEOUT_MS EQU 4000 ; inactivity limit (ms) for payment prompt

    ; Mapping tables for keypad conversions
    KEY_NUM_TABLE   DB 1,2,3,0FFH,4,5,6,0FFH,7,8,9,0FFH,0FFH,0,0FFH,0FFH
    KEY_ASCII_TABLE DB '1','2','3','?','4','5','6','?','7','8','9','?','*','0','#','?'

    ; Runtime state
    CURRENT_CODE    DB MAX_CODE_LEN DUP(0)
    CODE_LEN        DB 0
    SYSTEM_ARMED    DB 0
    LCD_ACTIVE      DB 0

    TEMP_CODE       DB MAX_CODE_LEN DUP(0)
    TEMP_LEN        DB 0

    ; Display strings (20x4 LCD assumed)
    MENU_LINE1      DB '      MAIN MENU      ','$'
    MENU_LINE2      DB '     [1] Set code     ','$'
    MENU_LINE3      DB '     [2] Arm system   ','$'
    MENU_LINE4      DB '        Choice?       ','$'

    PROMPT_SET_LINE1 DB '  Input Access Code   ','$'
    PROMPT_SET_LINE2 DB ' [        ]           ','$'
    PROMPT_SET_HELP  DB ' # to save, * clears  ','$'

    PROMPT_ARM_FLASH1 DB ' WARNING: System is   ','$'
    PROMPT_ARM_FLASH2 DB ' armed! press 0 to     ','$'
    PROMPT_ARM_FLASH3 DB ' disarm.              ','$'

    PROMPT_ENTER_CODE  DB ' Enter code then','$'
    PROMPT_ENTER_CODE2 DB ' press#','$'
    PROMPT_ENTER_LINE  DB ' [        ]           ','$'

    MSG_NO_CODE1    DB 'ERROR: System cannot','$'
    MSG_NO_CODE2    DB 'be armed, no code','$'
    MSG_NO_CODE3    DB 'set!','$'

    MSG_CODE_TOO_SHORT DB 'Must be 4-8 digits','$'

    MSG_ARMED_LINE1 DB 'System ARMED waiting  ','$'
    MSG_ARMED_LINE2 DB 'for disarm code...    ','$'

    MSG_BAD_CODE1   DB ' ERROR: Incorrect   ','$'
    MSG_BAD_CODE2   DB ' code. Try again.            ','$'

    MSG_SUCCESS1    DB ' SUCCESS: System is    ','$'
    MSG_SUCCESS2    DB ' disarmed!             ','$'
    MSG_SUCCESS3    DB ' press # to menu.      ','$'

    MSG_CODE_SAVED1 DB ' Code saved!           ','$'
    MSG_CODE_SAVED2 DB ' Returning to menu...  ','$'

    ; --- Grocery Locker specific definitions ---
    LCD_LINE1   EQU 080H
    LCD_LINE2   EQU 0C0H
    LCD_LINE3   EQU 094H
    LCD_LINE4   EQU 0D4H
    LOCK_RED_MASK   EQU 00000001B
    LOCK_GREEN_MASK EQU 00000010B
    SERVO_LOCK_MASK EQU 00000001B
    SERVO_UNLOCK_MASK EQU 00000000B
    PIT_CTRL_WORD EQU 00110100B
    PIT_COUNT_VALUE EQU 01H   ; Lower value = faster timer ticks for testing

    PIN_PROMPT_LINE  DB 'Enter 4-digit PIN     ','$'
    PIN_INPUT_LINE   DB 'PIN: ____           ','$'
    DISARM_PROMPT    DB 'Enter PIN to unlock','$'
    LOCK_LINE        DB 'Locker Secured        ','$'
    EXIT_PROMPT      DB 'Press * to finish     ','$'
    TIME_LINE        DB 'Time: 00:00:00        ','$'
    PAY_HEADER       DB 'Payment Mode          ','$'
    PAY_LINE         DB 'Cost:0000 Paid:0000   ','$'
    UNLOCK_LINE1     DB 'Payment complete!     ','$'
    UNLOCK_LINE2     DB 'Locker Unlocked       ','$'
    CHANGE_PROMPT    DB 'Your change is:       ','$'
    CHANGE_LINE      DB 'Change:0000           ','$'
    DISPENSING_MSG    DB 'Dispensing change...  ','$'
    PRESS_HASH_PROMPT DB 'Press # to continue   ','$'

    PIN_BUFFER       DB 4 DUP (0)
    PIN_LENGTH       DB 0
    SECONDS_COUNTER  DW 0
    COST_ACCUM       DW 0
    PAID_TOTAL       DW 0
    PIC_MASK         DB 0FFH
    LAST_MINUTE      DW 0
    LAST_PAYMENT_TOTAL DW 0
    COIN_PROMPT      DB 'Insert coins to pay   ','$'
    DUE_LINE         DB 'Due:0000           ','$'
    DEC_DIVISORS     DW 1000,100,10,1

DATA ENDS

STK SEGMENT
	 BOS DW 80d DUP (?)
	 TOS LABEL WORD
STK ENDS

PROCED1 SEGMENT 'CODE'
ASSUME CS:PROCED1, DS:DATA
ISR1 PROC FAR
    PUSHF
    PUSH AX
    PUSH DX
    PUSH DS
    MOV AX, DATA
    MOV DS, AX
    INC SECONDS_COUNTER
    POP DS
    POP DX
    POP AX
    POPF
    IRET
ISR1 ENDP
PROCED1 ENDS

PROCED2 SEGMENT 'CODE'
ASSUME CS:PROCED2, DS:DATA
ISR2 PROC FAR
    PUSHF
    PUSH AX
    PUSH DX
    PUSH DS
    MOV AX, DATA
    MOV DS, AX
    INC PAID_TOTAL
    POP DS
    POP DX
    POP AX
    POPF
    IRET
ISR2 ENDP
PROCED2 ENDS

PROCED3 SEGMENT 'CODE'
ASSUME CS:PROCED3, DS:DATA
ISR3 PROC FAR
    PUSHF
    PUSH AX
    PUSH DX
    PUSH DS
    MOV AX, DATA
    MOV DS, AX
    ADD PAID_TOTAL, 5
    POP DS
    POP DX
    POP AX
    POPF
    IRET
ISR3 ENDP
PROCED3 ENDS

PROCED4 SEGMENT 'CODE'
ASSUME CS:PROCED4, DS:DATA
ISR4 PROC FAR
    PUSHF
    PUSH AX
    PUSH DX
    PUSH DS
    MOV AX, DATA
    MOV DS, AX
    ADD PAID_TOTAL, 10
    POP DS
    POP DX
    POP AX
    POPF
    IRET
ISR4 ENDP
PROCED4 ENDS

PROCED5 SEGMENT 'CODE'
ASSUME CS:PROCED5, DS:DATA
ISR5 PROC FAR
    PUSHF
    PUSH AX
    PUSH DX
    PUSH DS
    MOV AX, DATA
    MOV DS, AX
    ADD PAID_TOTAL, 20
    POP DS
    POP DX
    POP AX
    POPF
    IRET
ISR5 ENDP
PROCED5 ENDS

CODE SEGMENT PUBLIC 'CODE'
    ASSUME CS:CODE, DS:DATA, SS:STK

START:
    MOV AX, DATA
    MOV DS, AX
    MOV AX, STK
    MOV SS, AX
    LEA SP, TOS
    CLI
   
    XOR AX, AX
    MOV ES, AX

    ; Initialize 8255 for LCD/keypad: Mode 0, A out, B out, C in
    MOV DX, COM_REG
    MOV AL, 089H
    OUT DX, AL

    ; Initialize auxiliary 8255 for LEDs/servo
    MOV DX, COM_REG_2
    MOV AL, 080H
    OUT DX, AL

    CALL INIT_PIC
    
     MOV AX, OFFSET ISR1
     MOV [ES:200H], AX
     MOV AX, SEG ISR1
     MOV [ES:202H], AX

     MOV AX, OFFSET ISR2
     MOV [ES:204H], AX
     MOV AX, SEG ISR2
     MOV [ES:206H], AX
	 
     MOV AX, OFFSET ISR3
     MOV [ES:208H], AX
     MOV AX, SEG ISR3
     MOV [ES:20AH], AX
	 
     MOV AX, OFFSET ISR4
     MOV [ES:20CH], AX
     MOV AX, SEG ISR4
     MOV [ES:20EH], AX
	 
     MOV AX, OFFSET ISR5
     MOV [ES:210H], AX
     MOV AX, SEG ISR5
     MOV [ES:212H], AX
    
    CALL INIT_PIT

    STI

    CALL LCD_TURN_ON
    MOV LCD_ACTIVE, 1
    CALL SET_UNLOCK_STATE

MAIN_LOOP:
    CALL PREPARE_NEW_SESSION
    CALL PROMPT_FOR_LOCK_CODE
    CALL SAVE_PIN_TO_CURRENT
    CALL BEGIN_SESSION
    CALL RUN_SESSION
    CALL HANDLE_DISARM_SEQUENCE
    JMP MAIN_LOOP

;-----------------------------------------------------------------
; Self-service locker control flow
;-----------------------------------------------------------------

PREPARE_NEW_SESSION PROC
    MOV SYSTEM_ARMED, 0
    MOV CODE_LEN, 0
    MOV BYTE PTR PIN_LENGTH, 0
    MOV WORD PTR SECONDS_COUNTER, 0
    MOV WORD PTR LAST_MINUTE, 0
    MOV WORD PTR COST_ACCUM, 1        ; was 5, start cost = 1
    MOV WORD PTR PAID_TOTAL, 0
    LEA DI, CURRENT_CODE
    MOV CX, MAX_CODE_LEN
    MOV AL, 0
    REP STOSB
    CALL RESET_PIN_BUFFER
    RET
PREPARE_NEW_SESSION ENDP

PROMPT_FOR_LOCK_CODE PROC
    CALL CLEAR_LCD
    CALL RESET_PIN_BUFFER
    MOV AL, LCD_LINE1
    LEA SI, PIN_PROMPT_LINE
    CALL PRINT_AT
    MOV AL, LCD_LINE2
    LEA SI, PIN_INPUT_LINE
    CALL PRINT_AT
    MOV AL, LCD_LINE3
    LEA SI, PROMPT_ENTER_CODE
    CALL PRINT_AT
    MOV AL, LCD_LINE4
    LEA SI, PROMPT_ENTER_CODE2
    CALL PRINT_AT
    CALL PIN_INPUT_LOOP
    RET
PROMPT_FOR_LOCK_CODE ENDP

PROMPT_FOR_DISARM_CODE PROC
DISARM_PROMPT_LOOP:
    CALL CLEAR_LCD
    CALL RESET_PIN_BUFFER
    MOV AL, LCD_LINE1
    LEA SI, DISARM_PROMPT
    CALL PRINT_AT
    MOV AL, LCD_LINE2
    LEA SI, PIN_INPUT_LINE
    CALL PRINT_AT
    MOV AL, LCD_LINE3
    LEA SI, PROMPT_ENTER_CODE2
    CALL PRINT_AT
    CALL PIN_INPUT_LOOP_TIMEOUT
    CMP AL, 0
    JE DISARM_TIMEOUT
    CALL VERIFY_PIN_BUFFER
    CMP AL, 1
    JE DISARM_READY
    CALL DISPLAY_BAD_CODE
    JMP DISARM_PROMPT_LOOP
DISARM_READY:
    MOV AL, 1
    RET
DISARM_TIMEOUT:
    XOR AL, AL
    RET
PROMPT_FOR_DISARM_CODE ENDP

PIN_INPUT_LOOP PROC
    CALL UPDATE_PIN_DISPLAY
PIN_WAIT_LOOP:
    CALL WAIT_FOR_KEY
    CMP AL, KEY_HASH
    JE PIN_CONFIRM
    CMP AL, KEY_STAR
    JE PIN_CLEAR
    MOV BL, AL
    CALL WAIT_KEY_RELEASE
    MOV AL, BL
    CALL KEY_TO_DIGIT
    CMP AL, 0FFH
    JE PIN_WAIT_LOOP
    XOR BX, BX
    MOV BL, PIN_LENGTH
    CMP BL, MIN_CODE_LEN
    JAE PIN_WAIT_LOOP
    LEA SI, PIN_BUFFER
    ADD SI, BX
    MOV [SI], AL
    INC PIN_LENGTH
    CALL UPDATE_PIN_DISPLAY
    JMP PIN_WAIT_LOOP

PIN_CLEAR:
    CALL WAIT_KEY_RELEASE
    CALL RESET_PIN_BUFFER
    CALL UPDATE_PIN_DISPLAY
    JMP PIN_WAIT_LOOP

PIN_CONFIRM:
    CALL WAIT_KEY_RELEASE
    MOV BL, PIN_LENGTH
    CMP BL, MIN_CODE_LEN
    JB PIN_WAIT_LOOP
    RET
PIN_INPUT_LOOP ENDP

PIN_INPUT_LOOP_TIMEOUT PROC
    CALL UPDATE_PIN_DISPLAY
PIN_TIMEOUT_WAIT:
    MOV CX, DISARM_IDLE_TIMEOUT_MS
    CALL WAIT_FOR_KEY_TIMEOUT
    JC PIN_TIMEOUT_KEY_READY
    XOR AL, AL
    RET
PIN_TIMEOUT_KEY_READY:
    CMP AL, KEY_HASH
    JE PIN_TIMEOUT_CONFIRM
    CMP AL, KEY_STAR
    JE PIN_TIMEOUT_CLEAR
    MOV BL, AL
    CALL WAIT_KEY_RELEASE
    MOV AL, BL
    CALL KEY_TO_DIGIT
    CMP AL, 0FFH
    JE PIN_TIMEOUT_WAIT
    XOR BX, BX
    MOV BL, PIN_LENGTH
    CMP BL, MIN_CODE_LEN
    JAE PIN_TIMEOUT_WAIT
    LEA SI, PIN_BUFFER
    ADD SI, BX
    MOV [SI], AL
    INC PIN_LENGTH
    CALL UPDATE_PIN_DISPLAY
    JMP PIN_TIMEOUT_WAIT

PIN_TIMEOUT_CLEAR:
    CALL WAIT_KEY_RELEASE
    CALL RESET_PIN_BUFFER
    CALL UPDATE_PIN_DISPLAY
    JMP PIN_TIMEOUT_WAIT

PIN_TIMEOUT_CONFIRM:
    CALL WAIT_KEY_RELEASE
    MOV BL, PIN_LENGTH
    CMP BL, MIN_CODE_LEN
    JB PIN_TIMEOUT_WAIT
    MOV AL, 1
    RET
PIN_INPUT_LOOP_TIMEOUT ENDP

RESET_PIN_BUFFER PROC
    MOV BYTE PTR PIN_LENGTH, 0
    LEA DI, PIN_BUFFER
    MOV CX, MIN_CODE_LEN
    MOV AL, 0
    REP STOSB
    LEA DI, PIN_INPUT_LINE + 5
    MOV CX, MIN_CODE_LEN
    MOV AL, '_'
    REP STOSB
    RET
RESET_PIN_BUFFER ENDP

UPDATE_PIN_DISPLAY PROC
    PUSH AX
    PUSH BX
    PUSH CX
    PUSH SI
    PUSH DI
    LEA DI, PIN_INPUT_LINE + 5
    MOV CX, MIN_CODE_LEN
    MOV AL, '_'
    REP STOSB
    XOR BX, BX
    MOV BL, PIN_LENGTH
    MOV CX, BX
    JCXZ PIN_DISPLAY_WRITE
    LEA DI, PIN_INPUT_LINE + 5
    MOV AL, '*'
    REP STOSB
PIN_DISPLAY_WRITE:
    MOV AL, LCD_LINE2
    LEA SI, PIN_INPUT_LINE
    CALL PRINT_AT
    POP DI
    POP SI
    POP CX
    POP BX
    POP AX
    RET
UPDATE_PIN_DISPLAY ENDP

SAVE_PIN_TO_CURRENT PROC
    PUSH CX
    LEA SI, PIN_BUFFER
    LEA DI, CURRENT_CODE
    MOV CX, MIN_CODE_LEN
    REP MOVSB
    MOV AL, MIN_CODE_LEN
    MOV CODE_LEN, AL
    POP CX
    RET
SAVE_PIN_TO_CURRENT ENDP

BEGIN_SESSION PROC
    CALL SET_LOCK_STATE
    MOV SYSTEM_ARMED, 1
    MOV WORD PTR SECONDS_COUNTER, 0
    MOV WORD PTR LAST_MINUTE, 0
    MOV WORD PTR COST_ACCUM, 1        ; was 5
    MOV WORD PTR PAID_TOTAL, 0
    CALL REFRESH_TIME_AND_PAYMENT_STRINGS
    CALL CLEAR_LCD
    MOV AL, LCD_LINE1
    LEA SI, LOCK_LINE
    CALL PRINT_AT
    MOV AL, LCD_LINE2
    LEA SI, EXIT_PROMPT
    CALL PRINT_AT
    MOV AL, LCD_LINE3
    LEA SI, TIME_LINE
    CALL PRINT_AT
    MOV AL, LCD_LINE4
    LEA SI, PAY_LINE
    CALL PRINT_AT
    RET
BEGIN_SESSION ENDP

SHOW_SESSION_SCREEN PROC
    CALL CLEAR_LCD
    MOV AL, LCD_LINE1
    LEA SI, LOCK_LINE
    CALL PRINT_AT
    MOV AL, LCD_LINE2
    LEA SI, EXIT_PROMPT
    CALL PRINT_AT
    CALL UPDATE_SESSION_DISPLAY
    RET
SHOW_SESSION_SCREEN ENDP

RUN_SESSION PROC
SESSION_LOOP:
    CALL UPDATE_TIMER_METRICS
    CALL UPDATE_SESSION_DISPLAY
    CALL POLL_FOR_DISARM
    CMP AL, 1
    JE SESSION_DONE
    JMP SESSION_LOOP
SESSION_DONE:
    RET
RUN_SESSION ENDP

HANDLE_DISARM_SEQUENCE PROC
DISARM_STAGE:
    CALL PROMPT_FOR_DISARM_CODE
    CMP AL, 1
    JE PAYMENT_STAGE
    CALL SHOW_SESSION_SCREEN
    CALL RUN_SESSION
    JMP DISARM_STAGE
PAYMENT_STAGE:
PAYMENT_RETRY:
    CALL WAIT_FOR_PAYMENT
    CMP AL, 1
    JE COMPLETE_STAGE
    CALL SHOW_SESSION_SCREEN
    CALL RUN_SESSION
    JMP PAYMENT_RETRY
COMPLETE_STAGE:
    CALL SHOW_CHANGE_MESSAGE
    CALL COMPLETE_UNLOCK
    RET
HANDLE_DISARM_SEQUENCE ENDP

UPDATE_TIMER_METRICS PROC
    PUSH AX
    PUSH BX
    PUSH CX
    PUSH DX
    PUSH SI
    MOV CX, 10
    CALL DELAY_MS
    INC SECONDS_COUNTER
    CALL APPLY_MINUTE_RATES
    POP SI
    POP DX
    POP CX
    POP BX
    POP AX
    RET
UPDATE_TIMER_METRICS ENDP

APPLY_MINUTE_RATES PROC
    PUSH AX
    PUSH DX
    MOV AX, SECONDS_COUNTER
    XOR DX, DX
    MOV BX, 60
    DIV BX              ; AX = minutes
    CMP AX, 10
    JB AR_COST_ONE
    SUB AX, 10          ; AX = minutes - 10
    MOV BX, 5
    XOR DX, DX
    DIV BX              ; AX = (minutes-10)/5
    ADD AX, 2           ; base cost after first 10 minutes
    JMP AR_STORE
AR_COST_ONE:
    MOV AX, 1
AR_STORE:
    MOV COST_ACCUM, AX
    POP DX
    POP AX
    RET
APPLY_MINUTE_RATES ENDP

UPDATE_SESSION_DISPLAY PROC
    PUSH AX
    PUSH BX
    PUSH CX
    PUSH DX
    PUSH SI
    PUSH DI
    CALL REFRESH_TIME_AND_PAYMENT_STRINGS
    MOV AL, LCD_LINE3
    LEA SI, TIME_LINE
    CALL PRINT_AT
    MOV AL, LCD_LINE4
    LEA SI, PAY_LINE
    CALL PRINT_AT
    POP DI
    POP SI
    POP DX
    POP CX
    POP BX
    POP AX
    RET
UPDATE_SESSION_DISPLAY ENDP

REFRESH_TIME_AND_PAYMENT_STRINGS PROC
    PUSH AX
    PUSH BX
    PUSH CX
    PUSH DX
    PUSH SI
    PUSH DI
    MOV AX, SECONDS_COUNTER
    XOR DX, DX
    MOV BX, 3600
    DIV BX
    MOV SI, AX
    MOV BX, DX
    MOV AX, SI
    CMP AX, 99
    JBE HOURS_OK
    MOV AX, 99
HOURS_OK:
    LEA DI, TIME_LINE + 6
    CALL WRITE_DEC2
    MOV AX, BX
    XOR DX, DX
    MOV CX, 60
    DIV CX
    MOV SI, AX
    MOV BX, DX
    LEA DI, TIME_LINE + 9
    CALL WRITE_DEC2
    MOV AX, BX
    LEA DI, TIME_LINE + 12
    CALL WRITE_DEC2
    MOV AX, COST_ACCUM
    LEA DI, PAY_LINE + 5
    CALL WRITE_DEC4
    MOV AX, PAID_TOTAL
    LEA DI, PAY_LINE + 15
    CALL WRITE_DEC4
    POP DI
    POP SI
    POP DX
    POP CX
    POP BX
    POP AX
    RET
REFRESH_TIME_AND_PAYMENT_STRINGS ENDP

WRITE_DEC4 PROC
    PUSH BX
    PUSH CX
    PUSH DX
    PUSH SI
    MOV DX, AX
    MOV SI, OFFSET DEC_DIVISORS
    MOV CX, 4
WRITE_DEC4_LOOP:
    MOV AX, DX
    XOR DX, DX
    MOV BX, [SI]
    DIV BX
    ADD AL, '0'
    MOV [DI], AL
    INC DI
    ADD SI, 2
    LOOP WRITE_DEC4_LOOP
    POP SI
    POP DX
    POP CX
    POP BX
    RET
WRITE_DEC4 ENDP

WRITE_DEC2 PROC
    PUSH BX
    PUSH DX
    MOV AH, 0
    MOV BL, 10
    DIV BL
    ADD AL, '0'
    MOV [DI], AL
    ADD AH, '0'
    MOV [DI + 1], AH
    POP DX
    POP BX
    RET
WRITE_DEC2 ENDP

POLL_FOR_DISARM PROC
    CALL SCAN_KEY
    JNC NO_DISARM_KEY
    CMP AL, KEY_STAR
    JNE RELEASE_OTHER
    CALL WAIT_KEY_RELEASE
    MOV AL, 1
    RET
RELEASE_OTHER:
    CALL WAIT_KEY_RELEASE
NO_DISARM_KEY:
    XOR AL, AL
    RET
POLL_FOR_DISARM ENDP

WAIT_FOR_PAYMENT PROC
    CALL CLEAR_LCD
    MOV AL, LCD_LINE1
    LEA SI, PAY_HEADER
    CALL PRINT_AT
    MOV AX, PAID_TOTAL
    MOV LAST_PAYMENT_TOTAL, AX
    MOV BX, PAYMENT_IDLE_TIMEOUT_MS
PAYMENT_LOOP:
    CALL UPDATE_PAYMENT_PROMPTS
    MOV AX, PAID_TOTAL
    CMP AX, COST_ACCUM
    JAE PAYMENT_DONE
    CMP AX, LAST_PAYMENT_TOTAL
    JNE PAYMENT_RESET_IDLE
    CMP BX, 50
    JBE PAYMENT_TIMEOUT
    SUB BX, 50
    JMP PAYMENT_DELAY
PAYMENT_RESET_IDLE:
    MOV LAST_PAYMENT_TOTAL, AX
    MOV BX, PAYMENT_IDLE_TIMEOUT_MS
PAYMENT_DELAY:
    MOV CX, 50
    CALL DELAY_MS
    JMP PAYMENT_LOOP
PAYMENT_DONE:
    MOV AL, 1
    RET
PAYMENT_TIMEOUT:
    XOR AL, AL
    RET
WAIT_FOR_PAYMENT ENDP

SHOW_CHANGE_MESSAGE PROC
    PUSH AX
    PUSH CX
    PUSH SI
    PUSH DI
    MOV AX, PAID_TOTAL
    SUB AX, COST_ACCUM
    JLE SCM_DONE                 ; No change due
    LEA DI, CHANGE_LINE + 7
    CALL WRITE_DEC4
    CALL CLEAR_LCD
    ; Line 1: status
    MOV AL, LCD_LINE1
    LEA SI, DISPENSING_MSG
    CALL PRINT_AT
    ; Line 2: change value
    MOV AL, LCD_LINE2
    LEA SI, CHANGE_LINE
    CALL PRINT_AT
    ; Line 4: prompt to continue
    MOV AL, LCD_LINE4
    LEA SI, PRESS_HASH_PROMPT
    CALL PRINT_AT

WAIT_HASH_LOOP:
    CALL WAIT_FOR_KEY
    CMP AL, KEY_HASH
    JE HASH_ACCEPT
    CALL WAIT_KEY_RELEASE
    JMP WAIT_HASH_LOOP
HASH_ACCEPT:
    CALL WAIT_KEY_RELEASE

SCM_DONE:
    POP DI
    POP SI
    POP CX
    POP AX
    RET
SHOW_CHANGE_MESSAGE ENDP

UPDATE_PAYMENT_PROMPTS PROC
    PUSH AX
    PUSH DX
    CALL REFRESH_TIME_AND_PAYMENT_STRINGS
    MOV AX, COST_ACCUM
    SUB AX, PAID_TOTAL
    JGE STORE_DUE
    XOR AX, AX
STORE_DUE:
    LEA DI, DUE_LINE + 4
    CALL WRITE_DEC4
    MOV AL, LCD_LINE2
    LEA SI, DUE_LINE
    CALL PRINT_AT
    MOV AL, LCD_LINE3
    LEA SI, PAY_LINE
    CALL PRINT_AT
    MOV AL, LCD_LINE4
    LEA SI, COIN_PROMPT
    CALL PRINT_AT
    POP DX
    POP AX
    RET
UPDATE_PAYMENT_PROMPTS ENDP

COMPLETE_UNLOCK PROC
    CALL SET_UNLOCK_STATE
    MOV SYSTEM_ARMED, 0
    CALL CLEAR_LCD
    MOV AL, LCD_LINE1
    LEA SI, UNLOCK_LINE1
    CALL PRINT_AT
    MOV AL, LCD_LINE2
    LEA SI, UNLOCK_LINE2
    CALL PRINT_AT
    MOV CX, MESSAGE_PAUSE_MS
    CALL DELAY_MS
    RET
COMPLETE_UNLOCK ENDP

VERIFY_PIN_BUFFER PROC
    MOV AL, CODE_LEN
    CMP AL, MIN_CODE_LEN
    JNE PIN_VERIFY_FAIL
    MOV CX, MIN_CODE_LEN
    LEA SI, CURRENT_CODE
    LEA DI, PIN_BUFFER
    REPE CMPSB
    JNZ PIN_VERIFY_FAIL
    MOV AL, 1
    RET
PIN_VERIFY_FAIL:
    XOR AL, AL
    RET
VERIFY_PIN_BUFFER ENDP

DISPLAY_BAD_CODE PROC
    CALL CLEAR_LCD
    MOV AL, LCD_LINE1
    LEA SI, MSG_BAD_CODE1
    CALL PRINT_AT
    MOV AL, LCD_LINE2
    LEA SI, MSG_BAD_CODE2
    CALL PRINT_AT
    MOV CX, MESSAGE_PAUSE_MS
    CALL DELAY_MS
    RET
DISPLAY_BAD_CODE ENDP

SET_LOCK_STATE PROC
    PUSH AX
    PUSH DX
    MOV DX, PORTA_2
    MOV AL, LOCK_RED_MASK
    OUT DX, AL
    MOV DX, PORTB_2
    MOV AL, SERVO_LOCK_MASK
    OUT DX, AL
    
    MOV DX, PORTC_2
    MOV AL, 1001B
    OUT DX, AL
    
    
    POP DX
    POP AX
    RET
SET_LOCK_STATE ENDP

SET_UNLOCK_STATE PROC
    PUSH AX
    PUSH DX
    MOV DX, PORTA_2
    MOV AL, LOCK_GREEN_MASK
    OUT DX, AL
    MOV DX, PORTB_2
    MOV AL, SERVO_UNLOCK_MASK
    OUT DX, AL
    
    MOV DX, PORTC_2
    MOV AL, 1100B
    OUT DX, AL
    
    POP DX
    POP AX
    RET
SET_UNLOCK_STATE ENDP

INIT_PIC PROC
    PUSH AX
    PUSH DX
    MOV DX, PIC1
    MOV AL, ICW1
    OUT DX, AL
    MOV DX, PIC2
    MOV AL, ICW2
    OUT DX, AL
    MOV AL, ICW4
    OUT DX, AL
    MOV AL, OCW1_MASK
    MOV PIC_MASK, AL
    OUT DX, AL
    POP DX
    POP AX
    RET
INIT_PIC ENDP

INIT_PIT PROC
    PUSH AX
    PUSH DX
    MOV DX, PIT_CTRL
    MOV AL, PIT_CTRL_WORD
    OUT DX, AL
    MOV DX, PIT_CH0
    MOV AX, PIT_COUNT_VALUE
    OUT DX, AL
    
    MOV AL, 00H
    OUT DX, AL
    POP DX
    POP AX
    RET
INIT_PIT ENDP

;-------------------------------------------------------------
; Hardware interface helpers
;-------------------------------------------------------------

PRINT_AT PROC
    PUSH AX
    PUSH SI
    CALL INST_CTRL
PRINT_AT_LOOP:
    LODSB
    CMP AL, '$'
    JE PRINT_AT_DONE
    CALL DATA_CTRL
    JMP PRINT_AT_LOOP
PRINT_AT_DONE:
    POP SI
    POP AX
    RET
PRINT_AT ENDP

CLEAR_LCD PROC
    MOV AL, 01H
    CALL INST_CTRL
    MOV CX, 2
    CALL DELAY_MS
    RET
CLEAR_LCD ENDP

LCD_TURN_ON PROC
    CALL INIT_LCD
    MOV LCD_ACTIVE, 1
    RET
LCD_TURN_ON ENDP

LCD_TURN_OFF PROC
    MOV AL, 08H
    CALL INST_CTRL
    MOV LCD_ACTIVE, 0
    RET
LCD_TURN_OFF ENDP

WAIT_FOR_KEY PROC
WAIT_KEY_LOOP:
    CALL SCAN_KEY
    JC WAIT_KEY_READY
    CALL DELAY_1MS
    JMP WAIT_KEY_LOOP
WAIT_KEY_READY:
    RET
WAIT_FOR_KEY ENDP

WAIT_FOR_KEY_TIMEOUT PROC
    ; Expects CX = timeout in milliseconds (0 disables timeout)
    PUSH BX
    CMP CX, 0
    JE WKT_INFINITE
WKT_LOOP:
    CALL SCAN_KEY
    JC WKT_READY
    CALL DELAY_1MS
    LOOP WKT_LOOP
    POP BX
    CLC
    RET
WKT_INFINITE:
    CALL WAIT_FOR_KEY
    POP BX
    STC
    RET
WKT_READY:
    POP BX
    STC
    RET
WAIT_FOR_KEY_TIMEOUT ENDP

WAIT_KEY_RELEASE PROC
WAIT_RELEASE_LOOP:
    CALL SCAN_KEY
    JNC WAIT_RELEASE_DONE
    CALL DELAY_1MS
    JMP WAIT_RELEASE_LOOP
WAIT_RELEASE_DONE:
    RET
WAIT_KEY_RELEASE ENDP

SCAN_KEY PROC
    PUSH DX
    MOV DX, PORTC
    IN AL, DX
    TEST AL, 10H
    JZ NO_KEY_PRESENT
    IN AL, DX
    AND AL, 0FH
    STC
    POP DX
    RET
NO_KEY_PRESENT:
    CLC
    POP DX
    RET
SCAN_KEY ENDP

HOLD_KEY PROC
    ; Expects CX = hold duration in DELAY_1MS ticks, DL = expected key nibble
    ; Returns AL=1 once the key stayed high for CX ticks (even if still pressed).
HOLD_LOOP:
    CALL SCAN_KEY
    JNC HOLD_FAIL
    CMP AL, DL
    JNE HOLD_FAIL
    CALL DELAY_1MS
    LOOP HOLD_LOOP
    MOV AL, 1
    RET
HOLD_FAIL:
    XOR AL, AL
    RET
HOLD_KEY ENDP

KEY_TO_DIGIT PROC
    ; Input AL = key nibble, Output AL = digit (0-9) or 0FFH if invalid
    PUSH BX
    MOV BL, AL
    XOR BH, BH
    MOV AL, KEY_NUM_TABLE[BX]
    POP BX
    RET
KEY_TO_DIGIT ENDP

DELAY_MS PROC
DELAY_MS_LOOP:
    CALL DELAY_1MS
    LOOP DELAY_MS_LOOP
    RET
DELAY_MS ENDP

INST_CTRL PROC
    PUSH AX
    PUSH DX
    MOV DX, PORTA
    OUT DX, AL
    MOV DX, PORTB
    MOV AL, 02H
    OUT DX, AL
    CALL DELAY_1MS
    MOV DX, PORTB
    MOV AL, 00H
    OUT DX, AL
    POP DX
    POP AX
    RET
INST_CTRL ENDP

DATA_CTRL PROC
    PUSH AX
    PUSH DX
    MOV DX, PORTA
    OUT DX, AL
    MOV DX, PORTB
    MOV AL, 03H
    OUT DX, AL
    CALL DELAY_1MS
    MOV DX, PORTB
    MOV AL, 01H
    OUT DX, AL
    POP DX
    POP AX
    RET
DATA_CTRL ENDP

INIT_LCD PROC
    MOV AL, 38H
    CALL INST_CTRL
    MOV AL, 08H
    CALL INST_CTRL
    MOV AL, 01H
    CALL INST_CTRL
    MOV CX, 2
    CALL DELAY_MS
    MOV AL, 06H
    CALL INST_CTRL
    MOV AL, 0CH
    CALL INST_CTRL
    RET
INIT_LCD ENDP

DELAY_1MS PROC
    PUSH BX
    MOV BX, 001AH    ;02CAH = irl 1 second (lower = faster)
L1:
    DEC BX
    NOP
    JNZ L1
    POP BX
    RET
DELAY_1MS ENDP

INIT_8253:
    MOV DX, PIT_CTRL
    MOV AL, 00110100B   ; Channel 0, LSB+MSB, Mode 0, Binary
    OUT DX, AL
    RET

CODE ENDS
END START
